
#pragma kernel CSMain
#include "Assets/Shaders/General/colorSpaces.cginc"

/*
This shader doesn't care how the data was created, 
it simply renders the image on the screen based on the parameters
TODO: document the variables
*/

RWTexture2D<float4> Result;
uint _ReduceAmount;
bool _Superresolution;
struct res {
	uint iter; 
	bool finished;
	float offset;
}; 
RWStructuredBuffer<res> _IterBuffer;
RWStructuredBuffer<res> _OldIterBuffer;
uint _MaxIter;
uint _OldPixelWidth;
bool _Upscaling;
bool _RenderExact;
int _ColorArrayLength;
StructuredBuffer<float4> _Colors;
bool _Smooth;
int _Type;
float _ColorStrength;
float4 getColor(res res) {
	float4 InColor = float4(0.0, 0.0, 0.0, 1.0f);


	if (!res.finished || res.iter >= _MaxIter) {
		return InColor;
	}
	if (res.iter == 0) {
		res.iter = 1;
	}
	float iter;
	if (_Smooth) {
		iter = (float)res.iter - res.offset;
	}
	else {
		iter = (float)res.iter;
	}
	float baseRatio = iter/_ColorStrength;
	int baseColorIdx = (int)(floor(baseRatio) % _ColorArrayLength);
	int upColorIdx = (int)((floor(baseRatio) + 1) % _ColorArrayLength);
	float ratio = baseRatio % 1;
	
	return lerpColors(_Colors[baseColorIdx],_Colors[upColorIdx], ratio,_Type,_Smooth);
	

	 
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	uint width, height;
	Result.GetDimensions(width, height);
	uint textureX = id.x;
	uint textureY = id.y;
	uint reducedWidth = width;
	if (_RenderExact) {
		Result[id.xy] = getColor(_IterBuffer[textureX + textureY * reducedWidth]);
		return;
	}
	if (_Superresolution) {
		textureX *= _ReduceAmount;
		textureY *= _ReduceAmount;
		reducedWidth *= _ReduceAmount;
	}
	else { 
		textureX /= _ReduceAmount;
		textureY /= _ReduceAmount;
		reducedWidth /= _ReduceAmount;
	}
	int textureIdx = textureX + textureY * reducedWidth;
	if (_Upscaling) {
		if (_IterBuffer[textureIdx].finished) {
			Result[id.xy] = getColor(_IterBuffer[textureIdx]);
		}
		else {
			int oldTextureX = id.x;
			int oldTextureY = id.y;
			uint oldReducedWidth = width;
			if (_Superresolution) {
				oldTextureX *= _OldPixelWidth;
				oldTextureY *= _OldPixelWidth;
				oldReducedWidth *= _OldPixelWidth;
			}
			else {
				oldTextureX /= _OldPixelWidth;
				oldTextureY /= _OldPixelWidth;
				oldReducedWidth /= _OldPixelWidth;
			}

			int oldTextureIdx = oldTextureX + oldTextureY * oldReducedWidth;
			Result[id.xy] = getColor(_OldIterBuffer[oldTextureIdx]);
		}
	}
	else {
		Result[id.xy] = getColor(_IterBuffer[textureIdx]);
	}
    
	
	
}
