#pragma kernel CSMain

#pragma multi_compile MANDELBROT BURNING_SHIP MANDEL3 MANDEL4

#pragma multi_compile Q_VERY_LOW Q_LOW Q_MED Q_HIGH Q_VERY_HIGH Q_ULTRA Q_ULTRA_HIGH Q_SUPER Q_MEGA Q_SUPER_MEGA


#if Q_VERY_LOW
#define PRECISION 4
#elif Q_LOW
#define PRECISION 6
#elif Q_MED
#define PRECISION 8
#elif Q_HIGH
#define PRECISION 10
#elif Q_VERY_HIGH
#define PRECISION 12
#elif Q_ULTRA
#define PRECISION 14
#elif Q_ULTRA_HIGH
#define PRECISION 16
#elif Q_SUPER
#define PRECISION 18
#elif Q_MEGA
#define PRECISION 20
#elif Q_SUPER_MEGA
#define PRECISION 24
#endif

#include "Assets/Shaders/General/fpnum.cginc"

#define BIGNUM 10000.0
RWTexture2D<float4> Result;

float2 _PixelOffset;

uint _MaxIter;
uint _IterPerCycle = 5;
int _BailoutRadius;
int _BailoutRadiusSQ;

StructuredBuffer<int> _DataBuffer;
static const int MiddleX = 0;
static const int MiddleY = PRECISION;
static const int Scale = PRECISION * 2;


static const int CurX = 0;
static const int CurY = PRECISION;

static const int iter = 2 * PRECISION;
static const int finished = 2 * PRECISION + 1;
static const int iterOffset = 2 * PRECISION + 2;
static const int angle = 2 * PRECISION + 3;
static const int PixelSize = 2 * PRECISION + 4;

uint _RenderWidth;
int _FrankensteinOffsetX;
int _FrankensteinOffsetY;

int _ShiftX;
int _ShiftY;

uint _Register;

RWStructuredBuffer<int> _MultiFrameData;

bool _reset;

struct res {
	uint iter;
	bool finished;
	float offset;
	float angle;
};
RWStructuredBuffer<res> _IterBuffer;


//This wierd Multiplication by BIGNUM and conversion to INT is done 
//beacause I need to encode float in range 0.0 - 1.0 in an INT datatype
//this allows to store everything in one array insed of makeing a seprate array for the offsets
int getOffset(float2 squares) {
	float logz = log(squares.x + squares.y) / 2.0;
	float res = log(logz / log(_BailoutRadius));
#if MANDELBROT || BURNING_SHIP
	res /= log(2.0);
#elif MANDEL3
	res /= log(3.0);
#elif MANDEL4
	res /= log(4.0);
#endif
	res = clamp(res, 0.0, 1.0) * BIGNUM;
	return (int)res;
}
uint getIter(uint2 pixel, uint width, uint height, uint idx) {
	digits cX;
	digits cY;
	digits zX;
	digits zY;
	digits squareX;
	digits squareY;
	digits scale;
	digits offset;
	digits middle;
	//Initialize values
	[unroll]
	for (int j = 0; j < PRECISION; j++) {
		zX.digits[j] = _MultiFrameData[idx + CurX + j];
		zY.digits[j] = _MultiFrameData[idx + CurY + j];
		scale.digits[j] = _DataBuffer[Scale + j];
		offset.digits[j] = 0;
	}
	//Initialize values from cpu
	offset.digits[0] = pixel.x - width / 2;
	offset = multiply(add(offset, setDouble((double)_PixelOffset.x)), scale);
	[unroll]
	for (int a = 0; a < PRECISION; a++) {
		middle.digits[a] = _DataBuffer[MiddleX + a];
	}
	cX = add(offset, middle);
	for (int b = 0; b < PRECISION; b++) {
		offset.digits[b] = 0;
	}
	offset.digits[0] = pixel.y - height / 2;
	offset = multiply(add(offset, setDouble((double)_PixelOffset.y)), scale);
	[unroll]
	for (int c = 0; c < PRECISION; c++) {
		middle.digits[c] = _DataBuffer[MiddleY + c];
	}
	cY = add(offset, middle);
	//Turned out the image was flipped this was added to reflip it
	//It might have introduced some hidden bugs
	cY = Negate(cY);

	int iter = 0;
	squareX = square(zX);
	squareY = square(zY);
	[fastopt]
	for (uint i = 0; i < _IterPerCycle; i++) {


//This part is responsibe for calculationg iteration insert new fractals here
#if MANDELBROT
		zY = add(cY, multiply(add(zX,zX), zY));
		zX = add(subtract(squareX, squareY), cX);
#elif BURNING_SHIP
		zY = add(cY, abs(multiply(add(zX, zX), zY)));
		zX = add(subtract(squareX, squareY), cX);
#elif MANDEL3 
// Not the optimal way but it is simple to write down
// TODO: unroll it and reduce the amount of multiplications
		complex z;
		z.x = zX;
		z.y = zY;
		z = mulComplex(mulComplex(z, z), z);
		zX = add(z.x, cX);
		zY = add(z.y, cY);
#elif MANDEL4
		complex z;
		z.x = zX;
		z.y = zY;
		z = mulComplex(mulComplex(z, z), mulComplex(z, z));
		zX = add(z.x, cX);
		zY = add(z.y, cY);
#endif

		squareX = square(zX);
		squareY = square(zY);
		iter++;


		if (!inBounds(squareX, squareY, _BailoutRadiusSQ)) {
			_MultiFrameData[idx + iterOffset] = getOffset(float2(toFloat(squareX), toFloat(squareY)));
			_MultiFrameData[idx + angle] = atan2(toFloat(zY),toFloat(zX))*BIGNUM;

			_MultiFrameData[idx + finished] = true;
			return iter;
		}
	}
	for (int l = 0; l < PRECISION; l++) {
		_MultiFrameData[idx + CurX + l] = zX.digits[l];
	}
	for (int k = 0; k < PRECISION; k++) {
		_MultiFrameData[idx + CurY + k] = zY.digits[k];
	}



	return iter;


}

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{

	uint width, height;
	Result.GetDimensions(width, height);
	
	_BailoutRadiusSQ = _BailoutRadius * _BailoutRadius;
	
	uint basePixelId = id.x + id.y * width + width * height * _Register;

	uint pixelId = basePixelId * PixelSize;

	int currentIter = 0;

	//check if further computatuin is nesesary
	if (!_MultiFrameData[pixelId + finished]) {
		currentIter = getIter(id.xy, width, height, pixelId);

		_MultiFrameData[pixelId + iter] += currentIter;
		if ((uint)_MultiFrameData[pixelId + iter] >= _MaxIter) {
			_MultiFrameData[pixelId + finished] = true;
		}


	}
	int iterId = id.x + _FrankensteinOffsetX + (id.y + _FrankensteinOffsetY) * _RenderWidth;

	_IterBuffer[iterId].iter = _MultiFrameData[pixelId + iter];
	_IterBuffer[iterId].finished = _MultiFrameData[pixelId + finished];
	_IterBuffer[iterId].offset = (float)(_MultiFrameData[pixelId + iterOffset]) / BIGNUM;
	_IterBuffer[iterId].angle = (float)(_MultiFrameData[pixelId + angle]) / BIGNUM;


}
